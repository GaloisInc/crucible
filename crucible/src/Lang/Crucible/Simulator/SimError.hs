-----------------------------------------------------------------------
-- |
-- Module           : Lang.Crucible.Simulator.SimError
-- Description      : Data structure the execution state of the simulator
-- Copyright        : (c) Galois, Inc 2014
-- License          : BSD3
-- Maintainer       : Joe Hendrix <jhendrix@galois.com>
-- Stability        : provisional
------------------------------------------------------------------------
{-# LANGUAGE ConstraintKinds #-}
{-# LANGUAGE DataKinds #-}
{-# LANGUAGE DeriveDataTypeable #-}
{-# LANGUAGE FlexibleContexts #-}
{-# LANGUAGE GADTs #-}
{-# LANGUAGE RankNTypes #-}
{-# LANGUAGE ScopedTypeVariables #-}
{-# LANGUAGE TypeFamilies #-}
{-# LANGUAGE TypeOperators #-}
module Lang.Crucible.Simulator.SimError (
    SimErrorReason(..)
  , SimError(..)
  , simErrorReasonMsg
  , ppSimError
  ) where

import Control.Exception
import Data.String
import Data.Typeable
import Text.PrettyPrint.ANSI.Leijen hiding ((<$>))

import What4.ProgramLoc

------------------------------------------------------------------------
-- SimError

-- | Class for exceptions generated by simulator.
data SimErrorReason
   = GenericSimError !String
   | Unsupported !String -- ^ We can't do that (yet?)
   | ReadBeforeWriteSimError !String -- FIXME? include relevant data instead of a string?
   | AssertFailureSimError !String
   | ResourceExhausted String
      -- ^ A loop iteration count, or similar resource limit,
      --   was exceeded.
 deriving (Typeable)

-- | This is the main data object describing an error that has
-- occurred during Simulation or in the solver-based discharge of
-- assertions gathered during simulation.  The location where the
-- error occurred, a call-stack (most recent call to oldest call), and
-- the reason for the error is provided.
data SimError
   = SimError
   { simErrorLoc :: !ProgramLoc
   , simErrorCallStack :: [ProgramLoc] -- [current frame, caller, ... ]
   , simErrorReason :: !SimErrorReason
   }
 deriving (Typeable)

simErrorReasonMsg :: SimErrorReason -> String
simErrorReasonMsg (GenericSimError msg) = msg
simErrorReasonMsg (Unsupported msg) = "Unsupported feature: " ++ msg
simErrorReasonMsg (ReadBeforeWriteSimError msg) = msg
simErrorReasonMsg (AssertFailureSimError msg) = msg
simErrorReasonMsg (ResourceExhausted msg) = "Resource exhausted: " ++ msg

instance IsString SimErrorReason where
  fromString = GenericSimError

instance Show SimErrorReason where
  show = simErrorReasonMsg

instance Show SimError where
  show = show . ppSimError

ppSimError :: SimError -> Doc
ppSimError er =
  let why = vcat (text <$> lines (show (simErrorReason er)))
      at = srcPos True $ simErrorLoc er
      bt = fmap (srcPos False) $ simErrorCallStack er
  in vcat $ why : at : bt
 where srcPos isFirst l = let h = if isFirst then "in" else "  "
                              f = show $ plFunction l
                              p = show $ plSourceLoc l
                          in text h <+> text f <+> text "at" <+> text p

instance Exception SimError
